buildscript {
    apply from: 'shared/versions.gradle'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.28.2"
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
    }
}

plugins {
  id "org.sonarqube" version "3.4.0.2513"
}

sonarqube {
  properties {
    property "sonar.projectKey", "NevMem_survey"
    property "sonar.organization", "nevmem"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'java'

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.41.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "backend/**/src/**/*.kt", "android/**/src/**/*kt", "shared/**/src/**/*kt", "util/**/src/**/*kt", "common/**/src/**/*kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "backend/**/src/**/*.kt", "android/**/src/**/*kt", "shared/**/src/**/*kt", "util/**/src/**/*kt", "common/**/src/**/*kt"
}

apply plugin: 'org.jetbrains.kotlin.jvm'
